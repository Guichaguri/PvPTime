buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = ver
group = 'com.guichaguri.pvptime'
archivesBaseName = 'PvPTime-Mod'

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = '1.12.2-14.23.0.2489'
    runDir = 'run'
    mappings = 'snapshot_20170624'
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade project(':common')
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from project(":common").sourceSets.main.output
    classifier = 'DEV'
}

build.dependsOn deobfJar

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
